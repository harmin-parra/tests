plugins {
    id 'java'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

ext {
    allureVersion = '2.29.1'
    playwrightVersion = '1.54.0'
    junitVersion = '5.13.4'
    cucumberVersion = '7.27.2'
}

dependencies {

    implementation "com.microsoft.playwright:playwright:${playwrightVersion}"
    implementation "org.apache.commons:commons-lang3:3.18.0"
    implementation "commons-io:commons-io:2.19.0"

    testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"
    testImplementation "io.cucumber:cucumber-junit-platform-engine:${cucumberVersion}"
    testImplementation platform("io.qameta.allure:allure-bom:${allureVersion}")
    testImplementation "io.qameta.allure:allure-cucumber7-jvm"
    testImplementation "io.qameta.allure:allure-junit-platform"
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.junit.platform:junit-platform-suite:1.13.4"
}

testing {
    suites {
        test {
            useJUnitJupiter()
        }
    }
}

test {
    useJUnitPlatform()
    systemProperties System.getProperties()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = "full"  // shows stack traces
        showCauses = true
        showExceptions = true
        showStackTraces = true
    }
}

tasks.register("playwrightInstall", JavaExec) {
    group = "verification"
    description = "Download Playwright browser binaries"

    classpath = sourceSets.test.runtimeClasspath
    mainClass.set("com.microsoft.playwright.CLI")
    args = ["install"]  // or "install", "firefox", etc.
}

test.dependsOn(tasks.named("playwrightInstall"))
